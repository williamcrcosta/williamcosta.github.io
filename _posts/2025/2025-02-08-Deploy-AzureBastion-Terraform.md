---
layout: post
title: 'Deploy Azure Bastion (Terraform)'
date: 2025-02-08 08:30:00 -0300
categories: [Network]
tags: [Azure, Network, Bastion, Terraform, PaaS]
slug: 'Deploy-Bastion-Terraform'
#image:
  #path: assets/img/01/image.gif
---

Fala galera!üëã

**Bem-vindo ao Blog Cloud Insights!** ‚òÅÔ∏è

Neste post, vamos explorar como realizar o Deploy do Azure Bastion via Terraform.

### Pr√©-Requisitos

Antes de come√ßarmos nosso laborat√≥rio, verifique se voc√™ possui:

- Uma conta do Azure com uma assinatura ativa.

> Caso voc√™ n√£o tenha uma subscription, voc√™ pode criar uma Trial. Mais informa√ß√µes consulte <a href="https://azure.microsoft.com/en-us/" target="_blank">aqui</a>.
{: .prompt-tip }

- Um Service Principal (um tipo de identidade no Azure usada para autentica√ß√£o) com permissionamento adequado.

> Obs.: Aqui estou utilizando um Service Principal com permiss√£o de "Global Administrator" no Entra ID e com a permiss√£o de Contributor na minha assinatura. Para saber como adicionar permiss√µes elevadas em uma conta, consulte <a href="https://learn.microsoft.com/en-us/entra/identity/role-based-access-control/manage-roles-portal?tabs=admin-center" target="_blank">aqui</a>.
{:.prompt-info}

- Ter o VSCode Instalado em seu Sistema Operacional Windows com as extens√µes Azure Terraform, Hashicorp Terraform e PowerShell.

> Caso n√£o tenha o VSCode Instalado, fa√ßa o Download do instalador <a href="https://code.visualstudio.com/sha/download?build=stable&os=win32-x64" target="_blank">aqui</a>.
{:.prompt-info}

- Ter o Terraform instalado no seu computador.

> Caso n√£o tenha o Terraform instalado, siga este <a href="https://cloudinsights.com.br/posts/Service-Principal-Terraform/#1-primeiro-passo-aqui-%C3%A9-realizar-o-download-da-%C3%BAltima-vers%C3%A3o-do-execut%C3%A1vel-do-terraform-para-windows" target="_blank">procedimento</a>.
{:.prompt-info}

### 1. Criar estrutura de Arquivos

- Crie uma nova pasta e abra o VSCode "Visual Studio Code" nela para come√ßar a configurar os recursos necess√°rios.
  - Vamos estruturar os arquivos do projeto para iniciar a cria√ß√£o dos recursos com Terraform. Crie a estrutura de arquivos abaixo:

![Folder-Structure](/assets/img/Lab01-Bastion/Bastion-TF/FolderStructure2.png){: .shadow .rounded-10}

### 2. Vamos iniciar codificando nossos arquivos

- Aqui vamos adicionar as informa√ß√µes do nosso ambiente no Azure.
  - *Precisaremos setar algumas vari√°veis de ambiente.*
- Adicione esse conte√∫do no arquivo ***powershell-credentials-azure.ps1***.

```powershell
$env:ARM_CLIENT_ID = "Client ID do seu SPN" # Aqui estou adicionando as informa√ß√µes do meu Service Principal que cont√©m a permiss√£o de Global Administrator no Entra ID
$env:ARM_TENANT_ID = "O ID do seu Tenant no EntraID"
$env:ARM_SUBSCRIPTION_ID = "ID da sua subscription"
$env:ARM_CLIENT_SECRET = "Secret do seu SPN" # Aqui estou adicionando as informa√ß√µes do meu Service Principal que cont√©m a permiss√£o de Global Administrator no Entra ID
```

- Aqui vamos adicionar as informa√ß√µes de Provider do AzureRM. Adicione o conte√∫do abaixo no arquivo ***provider.tf***.

```hcl
provider "azurerm" {
  # Configure the Azure Resource Manager provider
  features {} # Configuration options for the provider
}
```

- Aqui vamos adicionar as informa√ß√µes da vers√£o mais recente do Provider AzureRM e HTTP. Adicione o conte√∫do abaixo no arquivo ***version.tf***.

```hcl
# Configure Terraform para usar os provedores necess√°rios
terraform {
  required_providers {
    azurerm = {
      source  = "hashicorp/azurerm"
      version = "4.19.0"
    }
    http = {
      source  = "hashicorp/http"
      version = "3.4.5"
    }
  }
}
```

> *Se quiser consultar uma vers√£o mais recente, pode buscar nos links a seguir <a href="https://registry.terraform.io/providers/hashicorp/azurerm/latest" target="_blank">**AzureRM Provider**</a> e <a href="https://registry.terraform.io/providers/hashicorp/http/latest" target="_blank">**HTTP-Provider**</a>*.
  {: .prompt-tip }

- Agora vamos iniciar a codifica√ß√£o dos recursos que precisamos fazer o deploy. Vamos iniciar com as tags. Elas s√£o importantes para identificar os recursos no Portal da Azure. Adicione o conte√∫do abaixo no arquivo ***locals.tf***.

```hcl
# Tags padr√µes para os recursos criados pelo Terraform
locals {
  tags = {
    # Ambiente em que o recurso est√° sendo criado
    Environment = "Dev"
    # Nome do projeto que o recurso est√° sendo criado
    Project = "Bastion-Host"
    # Ferramenta que est√° gerenciando o recurso
    Managedby = "Terraform"
  }
}
```

- Agora vamos adicionar o c√≥digo para criar um Resource-Group. Adicione o c√≥digo abaixo no arquivo ***rg.tf***.

```hcl
# Crie um recurso do tipo azurerm_resource_group
# mais informa√ß√µes: https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/resource_group
resource "azurerm_resource_group" "rg-bastion-host" {
  name     = "rg-bastion-host"
  location = "East US 2"
  tags     = local.tags
}
```

- Agora vamos adicionar o c√≥digo para criar uma Virtual Network. Adicione o conte√∫do abaixo no arquivo ***vnet.tf***.

```hcl
# Crie um recurso do tipo azurerm_virtual_network
# mais informa√ß√µes: https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/virtual_network
resource "azurerm_virtual_network" "vnet" {
  name                = "vnet"
  resource_group_name = azurerm_resource_group.rg-bastion-host.name
  location            = azurerm_resource_group.rg-bastion-host.location
  address_space       = ["10.101.0.0/16"] # Espa√ßo de endere√ßamento da VNet
  tags                = local.tags
}

# Crie um recurso do tipo azurerm_subnet
# mais informa√ß√µes: https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/subnet
resource "azurerm_subnet" "subnet" {
  name                              = "subnet"
  resource_group_name               = azurerm_resource_group.rg-bastion-host.name
  virtual_network_name              = azurerm_virtual_network.vnet.name
  address_prefixes                  = ["10.101.10.0/24"] # Sub-rede criada na VNet
  private_endpoint_network_policies = "NetworkSecurityGroupEnabled"
}

# Crie um recurso do tipo azurerm_subnet para o Bastion-Host
# mais informa√ß√µes: https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/subnet
resource "azurerm_subnet" "bastion-subnet" {
  name                 = "AzureBastionSubnet"
  resource_group_name  = azurerm_resource_group.rg-bastion-host.name
  virtual_network_name = azurerm_virtual_network.vnet.name
  address_prefixes     = ["10.101.50.0/26"]
}
```

> *Para mais informa√ß√µes sobre a Azure Bastion Subnet, consulte o link a seguir: <a href="https://learn.microsoft.com/en-us/azure/bastion/configuration-settings#subnet" target="_blank">**Azure Bastion subnet**</a>.*
  {: .prompt-tip }

- Agora vamos adicionar o c√≥digo para criar uma NSG (Network Security Group) e uma rule. Adicione o conte√∫do abaixo no arquivo ***nsg.tf***.

```hcl
# Esse c√≥digo faz uma solicita√ß√£o HTTP para obter o endere√ßo IP p√∫blico da m√°quina que est√° executando o Terraform. √â como perguntar "Qual √© o meu IP?" e obter a resposta diretamente na sua configura√ß√£o do Terraform.

data "http" "myip" {
  url = "http://ipv4.icanhazip.com"
}

# Crie um recurso do tipo azurerm_network_security_group
# mais informa√ß√µes: https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/network_security_group
resource "azurerm_network_security_group" "nsg" {
  name                = "nsg"
  resource_group_name = azurerm_resource_group.rg-bastion-host.name
  location            = azurerm_resource_group.rg-bastion-host.location
  tags                = local.tags

  # permite que as requests sejam recebidas
  security_rule {
    name                       = "RDP-Inbound"
    description                = "Allow RDP Inbound"
    priority                   = 1000
    direction                  = "Inbound"
    access                     = "Allow"
    protocol                   = "Tcp"
    source_port_range          = "*"
    destination_port_range     = "3389"
    source_address_prefix      = chomp(data.http.myip.response_body)
    destination_address_prefix = azurerm_windows_virtual_machine.vm-win-server.private_ip_address
  }
}

# Associe o NSG ao Subnet
# mais informa√ß√µes: https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/subnet_network_security_group_association
resource "azurerm_subnet_network_security_group_association" "association" {
  subnet_id                 = azurerm_subnet.subnet.id
  network_security_group_id = azurerm_network_security_group.nsg.id
}
```

- Aqui vamos criar uma VM Windows Server para validar o acesso atrav√©s do Bastion Host. Adicione o conte√∫do abaixo no arquivo ***vm-win-server.tf***

> *Como o Bastion Host ser√° utilizado como ponte de acesso seguro e criptografado para esta VM, n√£o √© necess√°rio criar um IP p√∫blico para a VM. O acesso ser√° feito internamente atrav√©s do Bastion Host.*
{:.prompt-info}

```hcl
# Cria uma interface de rede para a VM
resource "azurerm_network_interface" "vm-win-server-nic" {
  name                = "vm-win-server-nic"
  location            = azurerm_resource_group.rg-bastion-host.location
  resource_group_name = azurerm_resource_group.rg-bastion-host.name

  ip_configuration {
    name                          = "vm-win-server-ipconfig"
    subnet_id                     = azurerm_subnet.subnet.id
    private_ip_address_allocation = "Dynamic"
  }
  tags = local.tags
}

# Cria a VM com o Windows Server
resource "azurerm_windows_virtual_machine" "vm-win-server" {
  name                  = "vm-win-server"
  resource_group_name   = azurerm_resource_group.rg-bastion-host.name
  location              = azurerm_resource_group.rg-bastion-host.location
  size                  = "Standard_B2ms"
  admin_username        = var.admin_username
  admin_password        = var.admin_password
  network_interface_ids = [azurerm_network_interface.vm-win-server-nic.id]
  tags                  = local.tags

  # Configura o disco do sistema operacional
  os_disk {
    caching              = "ReadWrite"
    disk_size_gb         = 128
    storage_account_type = "StandardSSD_LRS"
    name                 = "vm-win-server-os-disk"
  }

  # Configura a imagem do sistema operacional
  source_image_reference {
    publisher = "MicrosoftWindowsServer"
    offer     = "WindowsServer"
    sku       = "2025-datacenter-azure-edition"
    version   = "latest"
  }

  # Habilita o agente de provisionamento da VM
  provision_vm_agent = true

  # Configura o fuso hor√°rio da VM
  timezone = "E. South America Standard Time"

  # Habilita o hotpatching
  hotpatching_enabled = true

  # Configura o modo de patch
  patch_mode = "AutomaticByPlatform"

  # Habilita o TPM
  vtpm_enabled = true

  # Habilita o Secure Boot
  secure_boot_enabled = true
}
```

- Aqui vamos adicionar o c√≥digo das nossas vari√°veis que iremos utilizar. Adicione o conte√∫do abaixo no arquivo ***variables.tf***.

```hcl
# Vari√°veis para atribuir um usu√°rio e senha para a M√°quina Virtual Windows Server.
variable "admin_username" {
  description = "O nome de usu√°rio para acessar a m√°quina virtual. Ser√° usado para o acesso remoto via RDP."
  type        = string
  sensitive   = true
}

variable "admin_password" {
  description = "A senha para a m√°quina virtual. Ser√° usada para o acesso remoto via RDP."
  type        = string
  sensitive   = true
}
```

> Em vez de adicionar os valores das vari√°veis no arquivo `variables.tf`, vamos adicion√°-las como vari√°veis de ambiente ao executar o Terraform. Isso ajuda a manter dados sens√≠veis, como senhas, fora dos arquivos de configura√ß√£o do Terraform.
{: .prompt-warning }

- Valide e utilize o exemplo abaixo para criar as vari√°veis de ambiente do username e password. No terminal do VSCode adicione essas linhas abaixo:

```powershell
# Adicione aqui seu usu√°rio e senha para a M√°quina Virtual Windows Server.
# Essas vari√°veis ser√£o usadas para o acesso remoto via RDP.
$env:TF_VAR_admin_username="Adicione aqui seu usu√°rio"
$env:TF_VAR_admin_password="Adicione aqui sua senha"
```

<center> Voc√™ pode validar a execu√ß√£o pelo exemplo abaixo: </center>
![User+Password-Variable](/assets/img/Lab01-Bastion/Bastion-TF/User+Password-EnvironmentVariables.png){: .shadow .rounded-10}

- Aqui vamos adicionar alguns outputs para facilitar a visualiza√ß√£o dos resultados do deploy. Adicione o conte√∫do abaixo no arquivo ***output.tf***.

```hcl
# Sa√≠da do endere√ßo IP privado da interface de rede da m√°quina virtual
output "private_ip" {
  value = azurerm_network_interface.vm-win-server-nic.private_ip_address
}

# Sa√≠da do nome da m√°quina virtual
output "vm_name" {
  value = azurerm_windows_virtual_machine.vm-win-server.name
}
```

- Por fim, vamos adicionar o c√≥digo que vai criar o bastion host no azure. Adicione o conte√∫do abaixo no arquivo ***bastionhost.tf***.

```hcl
# Cria um recurso do tipo public_ip para ser usado pelo Bastion
# mais informa√ß√µes: https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/public_ip
resource "azurerm_public_ip" "bastion-pip" {
  name                = "bastion-pip"
  location            = azurerm_resource_group.rg-bastion-host.location
  resource_group_name = azurerm_resource_group.rg-bastion-host.name
  allocation_method   = "Static"
  tags                = local.tags
}

# Cria um recurso do tipo azure_bastion_host
# mais informa√ß√µes: https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/bastion_host
resource "azurerm_bastion_host" "bastion" {
  name                = "bastion"
  location            = azurerm_resource_group.rg-bastion-host.location
  resource_group_name = azurerm_resource_group.rg-bastion-host.name
  sku                 = "Standard"

  ip_configuration {
    name                 = "configuration"
    subnet_id            = azurerm_subnet.bastion-subnet.id
    public_ip_address_id = azurerm_public_ip.bastion-pip.id
  }
}
```

### 4. Inicializar o Terraform

J√° com todos os arquivos codificados, vamos iniciar o Terraform. No terminal do VSCode, digite o comando abaixo. O comando `terraform init` inicializa um novo ou existente diret√≥rio do Terraform configurando o ambiente, baixando os plugins e preparando o estado para implanta√ß√£o.

```powershell
terraform init
```

![Terraform-Init](/assets/img/Lab01-Bastion/Bastion-TF/TerraformInit2.png){: .shadow .rounded-10}

### 5. Validar se a configura√ß√£o est√° Correta

- Nesta etapa vamos executar o `terraform validate` para verificar se os arquivos de configura√ß√£o criados est√£o corretos em termos de sintaxe e l√≥gica. Ele n√£o faz altera√ß√µes nos recursos, apenas valida a configura√ß√£o. √â √∫til para identificar erros antes de executar o plano *terraform plan*. üòâ

```powershell
terraform validate
```

![Terraform-Validate](/assets/img/Lab01-Bastion/Bastion-TF/TerraformValidate2.png){: .shadow .rounded-10}

#### 6. Validar a formata√ß√£o dos Arquivos

- Agora vamos executar o `terraform fmt`. Este comando formata os arquivos de configura√ß√£o para seguir um estilo de c√≥digo consistente e leg√≠vel. Corrige indenta√ß√£o, alinhamento e outros aspectos de formata√ß√£o conforme as conven√ß√µes do Terraform. √â uma boa pr√°tica usar esse comando regularmente para manter a base de c√≥digo organizada.

```powershell
terraform fmt
```

![Terraform-FMT](/assets/img/Lab01-Bastion/Bastion-TF/TerraformFMT2.png){: .shadow .rounded-10}

#### 7. Criar as vari√°veis de ambiente para ser poss√≠vel se conectar na Azure

- Neste passo com o powershell aberto, dentro do VSCode, execute o comando abaixo. Ele vai executar o arquivo ***powershell-credentials-azure.ps1***.

```powershell
.\powershell-credentials-azure.ps1
```

<center> Notem na figura que eu validei se as vari√°veis foram adicionadas corretamente </center>
![Set-Azure-Variables](/assets/img/Lab01-Bastion/Bastion-TF/EnvironmentVariables2.png){: .shadow .rounded-10}

#### 8. Criar plano de Execu√ß√£o

- Seguido todos esses passos, agora vamos criar um plano, execute o comando `terraform plan -out bastionhost.out`. Ele cria um plano de execu√ß√£o e armazena em um arquivo. O arquivo my_vm-lnx.out √© criado para revisar o que ser√° feito sem aplicar as mudan√ßas.

```powershell
terraform plan -out bastionhost.out
```


> Na figura acima, estou apresentando somente a quantidade de recursos que ser√£o criados. Isso √© feito para proteger a seguran√ßa dos dados e evitar exposi√ß√£o desnecess√°ria de informa√ß√µes sens√≠veis, como o ID da subscription onde os recursos ser√£o criados. Note que ele j√° me traz as informa√ß√µes de outputs que criamos logo acima.
{: .prompt-danger }

#### 8. Fazer o deploy dos Recursos

- Agora, com o plano j√° executado, j√° sabemos quais recursos ser√£o criados. Vamos executar o `terraform apply bastionhost.out`.
  - Esse comando aplica as mudan√ßas especificadas no arquivo de plano gerado pelo comando `terraform plan`.

```powershell
terraform apply bastionhost.out
```

![Terraform-Apply](/assets/img/Lab01-Bastion/Bastion-TF/TerraformApply.png){: .shadow .rounded-10}

#### 09. Listar recursos criados pelo Terraform

- Vamos executar o comando `terraform state list`. Ele √© utilizado para listar todos os recursos gerenciados pelo estado do Terraform.
  - Este comando exibe uma lista de recursos que foram criados, atualizados ou destru√≠dos pelo Terraform e que est√£o sendo rastreados no arquivo tfstate.

```powershell
terraform state list
```

![Terraform-State-List](/assets/img/Lab01-Bastion/Bastion-TF/TerraformState.png){: .shadow .rounded-10}

#### 11. Validar recursos criados no Portal Azure

- A estrutura de recursos criada pode ser vista na figura abaixo.

![RG-Recursos](/assets/img/Lab01-Bastion/Bastion-TF/RG.png){: .shadow .rounded-10}

- Os detalhes da VM criada podem ser vistos na figura abaixo.

![VM-Portal](/assets/img/Lab01-Bastion/Bastion-TF/CreateVM.png){: .shadow .rounded-10}

- Pagina Overview do Azure Bastion Host.

![Bastion-Portal](/assets/img/Lab01-Bastion/Bastion-TF/BastionHost.png){: .shadow .rounded-10}

- A regra de seguran√ßa de rede (NSG) foi criada com origem somente meu IP p√∫blico e destino o IP da VM, como visto na figura abaixo.

![NSG-Rule](/assets/img/Lab01-Bastion/Bastion-TF/NSG-Rule.png){: .shadow .rounded-10}


#### 12. Acessando a VM com o Azure Bastion

- Nesta parte, com o usu√°rio e senha configurados como vari√°veis de ambiente, podemos conectar √† VM Windows Server utilizando o protocolo RDP atrav√©s do Azure Bastion Host.

  - Dentro do portal da Azure, localize a vm , clique em Overview e depois em Bastion. Voce pode conferir o passo-a-passo abaixo:

![VM-Access](/assets/img/Lab01-Bastion/Bastion-TF/VM-Access-Bastion.gif){: .shadow .rounded-10}

#### 13. Remover recursos criados com Terraform Destroy

- Agora que n√≥s criamos nossos recursos, testamos, vamos fazer a remo√ß√£o deles. O comando `terraform destroy` √© utilizado para destruir todos os recursos gerenciados pelo Terraform em sua configura√ß√£o. Isso significa que ele apagar√° todos os recursos da infraestrutura que foram criados ou gerenciados pelo Terraform.

> Esse comando √© √∫til quando voc√™ deseja desfazer todas as mudan√ßas aplicadas ou quando precisa limpar o ambiente.
{: .prompt-tip }

![Terraform-Destroy](/assets/img/Lab01-Bastion/Bastion-TF/TerraformDestroy.png){: .shadow .rounded-10}
![Terraform-Destroy2](/assets/img/Lab01-Bastion/Bastion-TF/TerraformDestroy2.png){: .shadow .rounded-10}

> √â importante lembrar que o comando terraform destroy apaga todos os recursos gerenciados pelo Terraform, ent√£o use-o com cautela, especialmente em ambientes de produ√ß√£o. Sempre revise o plano de destrui√ß√£o antes de confirmar para garantir que voc√™ n√£o est√° apagando algo por engano.
{: .prompt-danger }

### Resumo:

Este artigo detalha o processo de implanta√ß√£o do Azure Bastion utilizando o Terraform. O Azure Bastion √© um servi√ßo que proporciona acesso seguro e cont√≠nuo √†s suas m√°quinas virtuais diretamente pelo portal do Azure, eliminando a necessidade de endere√ßos IP p√∫blicos ou configura√ß√µes complexas de VPN. Ao longo deste guia, abordamos os pr√©-requisitos necess√°rios, a estrutura√ß√£o dos arquivos de configura√ß√£o e os comandos essenciais para efetuar o deploy de forma eficiente e segura.

### Conclus√£o:

A implementa√ß√£o do Azure Bastion via Terraform automatiza e simplifica o processo de provisionamento de acesso seguro √†s VMs no Azure. Ao definir a infraestrutura como c√≥digo, garantimos consist√™ncia, repetibilidade e facilidade de manuten√ß√£o no ambiente. Este m√©todo n√£o s√≥ reduz a probabilidade de erros manuais como tamb√©m agiliza o processo de implanta√ß√£o, tornando-o mais robusto e escal√°vel.

### Pr√≥ximos Passos:

Para aprofundar seus conhecimentos e aprimorar a seguran√ßa e efici√™ncia do seu ambiente Azure, considere os seguintes passos:

1. **Explorar Configura√ß√µes Avan√ßadas do Azure Bastion**: Investigue recursos adicionais, como a integra√ß√£o com o Azure Active Directory e o uso de autentica√ß√£o multifator para refor√ßar a seguran√ßa.

2. **Automatizar Implanta√ß√µes com Pipelines CI/CD**: Implemente pipelines de Integra√ß√£o Cont√≠nua/Entrega Cont√≠nua para automatizar o deploy das configura√ß√µes do Terraform, garantindo atualiza√ß√µes consistentes e controladas.

3. **Monitorar e Registrar Acessos**: Configure o Azure Monitor e o Azure Log Analytics para acompanhar e registrar as atividades de acesso atrav√©s do Bastion, facilitando auditorias e detec√ß√£o de anomalias.

4. **Manter-se Atualizado com as Melhores Pr√°ticas de Seguran√ßa**: Revise regularmente as diretrizes de seguran√ßa do Azure e do Terraform para assegurar que suas implementa√ß√µes estejam alinhadas com as pr√°ticas recomendadas mais recentes.

Seguindo esses passos, voc√™ estar√° no caminho certo para otimizar a gest√£o e a seguran√ßa do seu ambiente de nuvem no Azure.

*Essas s√£o apenas algumas ideias para aprimorar sua infraestrutura com Terraform! Continue explorando novas configura√ß√µes e torne seu ambiente ainda mais seguro e automatizado.*

At√© a pr√≥xima!! üòâ

![resource-group](/assets/img/02/cloudinsights3.png){: .shadow .rounded-10}

#CloudInsights #Azure #Tech #Cloud #Security #Network #Terraform #InfraAsCode #PaaS #Bastion

---

[![Build and Deploy](https://github.com/williamcrcosta/williamcosta.github.io/actions/workflows/pages-deploy.yml/badge.svg)](https://github.com/williamcrcosta/williamcosta.github.io/actions/workflows/pages-deploy.yml)