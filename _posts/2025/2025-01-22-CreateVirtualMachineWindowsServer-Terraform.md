---
layout: post
title: 'Deploy VM WindowsServer2025 com Terraform'
date: 2025-01-20 08:30:00 -0300
categories: [IaaS]
tags: [Azure, IaaS, WindowsServer, Terraform]
slug: 'Deploy-VM-WindowsServer-Terraform'
#image:
  #path: assets/img/Lab02-ServicePrincipal/ServicePrincipal.webp
---

Fala galera!üëã

**Bem-vindo ao Blog Cloud Insights!** ‚òÅÔ∏è

Neste post, vamos explorar como automatizar o deploy de uma m√°quina virtual "***Windows Server 2025***" no Azure usando Terraform. A automa√ß√£o desse processo oferece v√°rios benef√≠cios, como:

- Consist√™ncia: Elimina erros manuais ao criar recursos no Azure.
- Efici√™ncia: Reduz o tempo de configura√ß√£o e implanta√ß√£o.
- Versionamento: Com o Terraform, voc√™ pode rastrear e controlar altera√ß√µes no ambiente de infraestrutura.

### Maquina Virtual ou Virtual Machine

**O que √©**:
Uma VM (Virtual Machine) √© um servi√ßo que oferece servidores virtuais sob demanda na nuvem. Esses servidores podem ser configurados para rodar sistemas operacionais como Windows ou Linux, al√©m de aplica√ß√µes espec√≠ficas. √â como ter um servidor f√≠sico, mas sem a necessidade de gerenciar o hardware diretamente, j√° que tudo √© virtualizado.

## Por que usar uma VM?

- **Elimina√ß√£o de Custos Fixos**: Substitui investimentos em servidores f√≠sicos por custos vari√°veis baseados no uso.
- **Redu√ß√£o de Complexidade**: O Azure ou qualquer Cloud Provider cuida da infraestrutura b√°sica, permitindo que voc√™ foque no software e nos servi√ßos.
- **Globaliza√ß√£o**: Implante VMs em diferentes regi√µes do mundo para garantir baixa lat√™ncia e atender a requisitos locais.
- **Backup e Escalabilidade**: Recursos para redimensionar ou replicar rapidamente conforme a necessidade.

## Para que serve

1. **Execu√ß√£o de Aplica√ß√µes**: Ideal para hospedar aplica√ß√µes empresariais que exigem um servidor dedicado.
2. **Ambiente de Testes**: Permite criar ambientes isolados para testes de software e experimentos.
3. **Armazenamento Tempor√°rio**: Processar ou hospedar dados em per√≠odos curtos sem ocupar recursos f√≠sicos.
4. **Ambiente de Desenvolvimento**: Para equipes de desenvolvimento que precisam de m√°quinas personalizadas.

### Cen√°rios de Uso

1¬∫ **Desenvolvimento e Teste de Software**:

- Criar ambientes de desenvolvimento semelhantes aos de produ√ß√£o.
- Testar diferentes sistemas operacionais ou configura√ß√µes.

2¬∫ **Hospedagem de Aplica√ß√µes Web**:

- Servir websites ou APIs com alta disponibilidade.
- Usar VMs como backend para aplica√ß√µes cr√≠ticas.

3¬∫ **Computa√ß√£o Intensiva**:

- Processamento de big data, an√°lise de dados ou execu√ß√£o de simula√ß√µes complexas.
- Treinamento de modelos de Machine Learning.

4¬∫ **Sistemas Herdados**:

- Migrar sistemas legados (on-premises) para a nuvem sem necessidade de reformula√ß√£o imediata.

5¬∫ **Extens√£o de Data Centers**:

- Usar VMs como uma extens√£o de data centers locais para lidar com demandas sazonais ou picos de carga.

6¬∫ **Recupera√ß√£o de Desastres**:

- Configurar VMs como backup de infraestrutura cr√≠tica para garantir continuidade em caso de falhas.

### Pr√©-Requisitos

Antes de come√ßarmos nosso laborat√≥rio, verifique se voc√™ possui:

- Uma conta do Azure com uma assinatura/subscription ativa.

> Caso voc√™ n√£o tenha uma subscription, voc√™ pode criar uma Trial. Mais informa√ß√µes consulte <a href="https://azure.microsoft.com/en-us/" target="_blank">aqui</a>.
{: .prompt-tip }

- Um Service Principal com permissionamento adequado.

> Obs.: Aqui estou utilizando um Service Principal com permiss√£o de "Global Administrator" no EntraID e com a permissao de Contributor na minha subscription. Para saber como adicionar uma permiss√£o privilegiada em uma conta, consulte <a href="https://learn.microsoft.com/en-us/entra/identity/role-based-access-control/manage-roles-portal?tabs=admin-center" target="_blank">aqui</a>.
{:.prompt-info}

- Ter o VSCode Instalado em seu Sistema Operacional Windows com as extens√µes Azure Terraform, Hashicorp Terraform e PowerShell.

> Caso n√£o tenha o VSCode Instalado, fa√ßa o Download do instalador <a href="https://code.visualstudio.com/sha/download?build=stable&os=win32-x64" target="_blank">aqui</a>.
{:.prompt-info}

- Ter o Terraform instalado no seu computador.

> Caso n√£o tenha o Terraform instalado, siga este <a href="https://cloudinsights.com.br/posts/Service-Principal-Terraform/#1-primeiro-passo-aqui-%C3%A9-realizar-o-download-da-%C3%BAltima-vers%C3%A3o-do-execut%C3%A1vel-do-terraform-para-windows" target="_blank">procedimento</a>.
{:.prompt-info}


### 1. Criar estrutura de Arquivos

- Crie uma nova pasta e abra o VSCode nela para come√ßar a configurar os recursos necess√°rios.
  - Vamos estruturar os arquivos do projeto para iniciar a cria√ß√£o dos recursos com Terraform. Crie a estrutura de arquivos a abaixo:

![Folder-Structure](/assets/img/Lab03-VMWindowsServer/FolderStructure.png){: .shadow .rounded-10}

### 2. Vamos iniciar codificando nossos arquivos

- Aqui vamos adicionar as informa√ß√µes do nosso ambiente no Azure.
  - *Precisaremos setar algumas vari√°veis de ambiente.*
- Adicione esse conteudo no arquivo ***powershell-credencials-azure.ps1***.

```powershell
$env:ARM_CLIENT_ID = "Client ID do seu SPN" # Aqui estou adicionando as informa√ß√µes do meu Service Principal que cont√©m a permiss√£o de Global Administrator no EntraID
$env:ARM_TENANT_ID = "O ID do seu Tenant no EntraID"
$env:ARM_SUBSCRIPTION_ID = "ID da sua subscription"
$env:ARM_CLIENT_SECRET = "Secret do seu SPN" # Aqui estou adicionando as informa√ß√µes do meu Service Principal que cont√©m a permiss√£o de Global Administrator no EntraID
```

- Aqui vamos adicionar as informa√ß√µes de Provider do AzureRM. Adicione o conteudo abaixo no arquivo ***provider.tf***.

```hcl
provider "azurerm" {
  # Configure the Azure Resource Manager provider
  features {} # Configuration options for the provider
}
```

- Aqui vamos adicionar as informa√ß√µes da vers√£o mais recente do Provider AzureRM e HTTP. Adicione o conteudo abaixo no arquivo ***version.tf***.

```hcl
# Configure Terraform para usar os provedores necess√°rios
terraform {
  required_providers {
    azurerm = {
      source  = "hashicorp/azurerm"
      version = "4.16.0"
    }
    http = {
      source  = "hashicorp/http"
      version = "3.4.5"
    }
  }
}
```

> *Se quiser consultar uma vers√£o mais recente, pode buscar nos links a seguir <a href="https://registry.terraform.io/providers/hashicorp/azurerm/latest" target="_blank">**AzureRM Provider**</a> e <a href="https://registry.terraform.io/providers/hashicorp/http/latest" target="_blank">**HTTP-Provider**</a>*.
  {: .prompt-tip }

- Agora vamos iniciar a codifica√ß√£o dos recursos que precisamos fazer o deploy. Vamos iniciar com as tags. Ela √© importante para identificar os recursos no Portal da Azure. Adicione o conte√∫do abaixo no arquivo ***locals.tf***.

```hcl
# Tags padr√µes para os recursos criados pelo Terraform
locals {
  tags = {
    # Ambiente em que o recurso esta sendo criado
    Environment = "Dev"
    # Nome do projeto que o recurso esta sendo criado
    Project = "VM-WindowsServer"
    # Ferramenta que esta gerenciando o recurso
    Managedby = "Terraform"
  }
}
```

- Agora vamos adicionar o c√≥digo para criar um Resource-Group. Adicione o c√≥digo abaixo no arquivo ***rg.tf***.

```hcl
# Crie um recurso do tipo resource_group
# mais informacoes: https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/resource_group
resource "azurerm_resource_group" "rg-vm-win" {
  name     = "rg-vm-win"
  location = "East US 2"
  tags     = local.tags
}
```

- Agora vamos adicionar o c√≥digo para criar uma Virtual Network. Adicione o conteudo abaixo no arquivo ***vnet.tf***.

```hcl
# Crie um recurso do tipo virtual_network
# mais informacoes: https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/virtual_network
resource "azurerm_virtual_network" "vnet" {
  name                = "vnet"
  resource_group_name = azurerm_resource_group.rg-vm-win.name
  location            = azurerm_resource_group.rg-vm-win.location
  address_space       = ["10.101.0.0/16"] # Espaco de enderecamento da VNet
  tags                = local.tags
}

# Crie um recurso do tipo subnet
# mais informacoes: https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/subnet
resource "azurerm_subnet" "subnet" {
  name                              = "subnet"
  resource_group_name               = azurerm_resource_group.rg-vm-win.name
  virtual_network_name              = azurerm_virtual_network.vnet.name
  address_prefixes                  = ["10.101.10.0/24"] # Sub-rede criada na VNet
  private_endpoint_network_policies = "NetworkSecurityGroupEnabled"
}
```

- Agora vamos adicionar o c√≥digo para criar uma NSG (Network Security Group) e uma rule. Adicione o conte√∫do abaixo no arquivo ***nsg.tf***.

```hcl
# Esse c√≥digo faz uma solicita√ß√£o HTTP para obter o endere√ßo IP p√∫blico da m√°quina que est√° executando o Terraform. √â como perguntar "Qual √© o meu IP?" e obter a resposta diretamente na sua configura√ß√£o do Terraform.
data "http" "myip" {
  url = "http://ipv4.icanhazip.com"
}

# Cria um recurso do tipo network_security_group
# mais informacoes: https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/network_security_group
resource "azurerm_network_security_group" "nsg" {
  name                = "nsg"
  resource_group_name = azurerm_resource_group.rg-vm-win.name
  location            = azurerm_resource_group.rg-vm-win.location
  tags                = local.tags

  # Permite que as requisi√ß√µes sejam recebidas somente a partir do meu IP p√∫blico, garantindo que apenas eu consiga acessar a VM.
  security_rule {
    name                       = "RDPInbound"
    priority                   = 100
    direction                  = "Inbound"
    access                     = "Allow"
    protocol                   = "*"
    source_port_range          = "*"
    destination_port_range     = "3389"
    source_address_prefix      = chomp(data.http.myip.response_body)
    destination_address_prefix = azurerm_windows_virtual_machine.vm-win-server.private_ip_address # Aqui no destino voc√™ garante que a origem vai conseguir comunica√ß√£o na porta 3389 somente no IP Privado da VM que vamos criar.
  }
}

# Associa o NSG √† Subnet
# mais informacoes: https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/subnet_network_security_group_association
resource "azurerm_subnet_network_security_group_association" "association" {
  subnet_id                 = azurerm_subnet.subnet.id
  network_security_group_id = azurerm_network_security_group.nsg.id
}

```

- Aqui vamos adicionar o c√≥digo das nossas vari√°veis que iremos utilizar. Adicione o conte√∫do abaixo no arquivo ***variables.tf***.

> Em vez de adicionar os valores das vari√°veis no arquivo `variables.tf`, vamos adicion√°-las como vari√°veis de ambiente ao executar o Terraform. Isso ajuda a manter dados sens√≠veis, como senhas, fora dos arquivos de configura√ß√£o do Terraform.
{: .prompt-warning }

```hcl
# Vari√°veis para atribuir um usu√°rio e senha para a Maquina Virtual Windows Server.
variable "admin_username" {
  description = "O nome de usu√°rio para acessar a maquina virtual. Ser√° usado para o acesso remoto via RDP."
  type        = string
}

variable "admin_password" {
  description = "A senha para a m√°quina virtual. Ser√° usada para o acesso remoto via RDP."
  type        = string
  sensitive   = true
}
```

- Agora vamos adicionar o c√≥digo para a estrutura da VM. Adicione o conte√∫do abaixo no arquivo ***vm-win-server.tf***.

```hcl
# Cria um IP publico para a VM
resource "azurerm_public_ip" "vm-win-server-pip" {
  name                = "vm-win-server-pip"
  resource_group_name = azurerm_resource_group.rg-vm-win.name
  location            = azurerm_resource_group.rg-vm-win.location
  allocation_method   = "Static"
  sku                 = "Standard"
  tags                = local.tags
}

# Cria uma interface de rede para a VM
resource "azurerm_network_interface" "vm-win-server-nic" {
  name                = "vm-win-server-nic"
  location            = azurerm_resource_group.rg-vm-win.location
  resource_group_name = azurerm_resource_group.rg-vm-win.name

  ip_configuration {
    name                          = "vm-win-server-ipconfig"
    subnet_id                     = azurerm_subnet.subnet.id
    private_ip_address_allocation = "Dynamic"
    public_ip_address_id          = azurerm_public_ip.vm-win-server-pip.id
  }
  tags = local.tags
}

# Cria a VM com o Windows Server
resource "azurerm_windows_virtual_machine" "vm-win-server" {
  name                  = "vm-win-server"
  resource_group_name   = azurerm_resource_group.rg-vm-win.name
  location              = azurerm_resource_group.rg-vm-win.location
  size                  = "Standard_B2ms"
  admin_username        = var.admin_username
  admin_password        = var.admin_password
  network_interface_ids = [azurerm_network_interface.vm-win-server-nic.id]
  tags                  = local.tags

  # Configura o disco do sistema operacional
  os_disk {
    caching              = "ReadWrite"
    disk_size_gb         = 128
    storage_account_type = "StandardSSD_LRS"
    name                 = "vm-win-server-os-disk"
  }

  # Configura a imagem do sistema operacional
  source_image_reference {
    publisher = "MicrosoftWindowsServer"
    offer     = "WindowsServer"
    sku       = "2025-datacenter-azure-edition"
    version   = "latest"
  }

  # Habilita o agente de provisionamento da VM
  provision_vm_agent = true

  # Configura o fuso horario da VM
  timezone = "E. South America Standard Time"

  # Habilita o hotpatching
  hotpatching_enabled = true

  # Configura o modo de patch
  patch_mode = "AutomaticByPlatform"

  # Habilita o TPM
  vtpm_enabled = true

  # Habilita o Secure Boot
  secure_boot_enabled = true
}
```

- Por fim, vamos adicionar alguns outputs para facilitar a visualiza√ß√£o dos resultados do deploy. Adicione o conteudo abaixo no arquivo ***output.tf***.

```hcl
output "public_ip" {
  value = azurerm_public_ip.vm-win-server-pip.ip_address
}

output "private_ip" {
  value = azurerm_network_interface.vm-win-server-nic.private_ip_address
}

output "vm_name" {
  value = azurerm_windows_virtual_machine.vm-win-server.name
}
```

![Terraform-Output](/assets/img/Lab03-VMWindowsServer/TerraformOutput.png){: .shadow .rounded-10}

### 4. Inicializar o Terraform

 J√° com todos os arquivos codificados, vamos iniciar o Terraform. No terminal do VSCode, digite o comando abaixo. O comando `terraform init` inicializa um novo ou existente diret√≥rio do Terraform configurando o ambiente, baixando os plugins e preparando o estado para implanta√ß√£o.

```powershell
terraform init
```

![Terraform-Init](/assets/img/Lab03-VMWindowsServer/TerraformInit.png){: .shadow .rounded-10}

### 5. Validar se a configura√ß√£o est√° Correta

- Nesta etapa vamos executar o `terraform validate` para verificar se os arquivos de configura√ß√£o criados est√£o corretos em termos de sintaxe e l√≥gica. Ele n√£o faz altera√ß√µes nos recursos, apenas valida a configura√ß√£o. √â √∫til para identificar erros antes de executar o plano *terraform plan*. üòâ

```powershell
terraform validate
```

![Terraform-Validate](/assets/img/Lab03-VMWindowsServer/TerraformValidate.png){: .shadow .rounded-10}

#### 6. Validar a formata√ß√£o dos Arquivos

- Agora vamos executar o `terraform fmt`. Este comando formata os arquivos de configura√ß√£o para seguir um estilo de c√≥digo consistente e leg√≠vel. Corrige indenta√ß√£o, alinhamento e outros aspectos de formata√ß√£o conforme as conven√ß√µes do Terraform. √â uma boa pr√°tica usar esse comando regularmente para manter a base de c√≥digo organizada.

```powershell
terraform fmt
```

![Terraform-FMT](/assets/img/Lab03-VMWindowsServer/TerraformFMT.png){: .shadow .rounded-10}

#### 7. Criar as vari√°veis de ambiente para ser possivel se conectar na Azure

- Neste passo com o powershell aberto, dentro do VSCode, execute o comando abaixo. Ele vai executar o arquivo ***powershell-credencials-azure.ps1***.

```powershell
.\powershell-credencials-azure.ps1
```

<center> Notem na figura que eu validei se as vari√°veis foram adicionadas corretamente </center>
![Set-Azure-Variables](/assets/img/Lab02-ServicePrincipal/SetVariaveisAzure.png){: .shadow .rounded-10}

- Aqui vamos adicionar as variavies de ambiente informando o usuario e senha da Virtual Machine. Adicione o comando abaixo em seu terminal do PowerShell.

```powershell
$env:TF_VAR_admin_username="admin.cloudinsights"

$env:TF_VAR_password="Crie uma senha e adicione aqui"
```

![Set-VM-Variables](/assets/img/Lab03-VMWindowsServer/VariaveisUserPass.png){: .shadow .rounded-10}

#### 8. Criar plano de Execu√ß√£o

- Seguido todos esses passos, agora vamos criar um plano, execute o comando `terraform plan -out my_vm.out`. Ele cria um plano de execu√ß√£o e armazena em um arquivo. O arquivo my_vm.out √© criado para revisar o que ser√° feito sem aplicar as mudan√ßas.

```powershell
terraform plan -out my_vm.out
```

![Terraform-Plan](/assets/img/Lab03-VMWindowsServer/TerraformPlan.png){: .shadow .rounded-10}

> Na figura acima, estou apresentando somente a quantidade de recursos que ser√£o criados. Isso √© feito para proteger a seguran√ßa dos dados e evitar exposi√ß√£o desnecess√°ria de informa√ß√µes sens√≠veis, como o ID da subscription onde os recursos ser√£o criados. Note que ele j√° me traz as informa√ß√µes de outputs que criamos logo acima.
{: .prompt-danger }

#### 8. Fazer o deploy dos Recursos

- Agora, com o plano j√° executado, j√° sabemos quais recursos ser√£o criados. Vamos executar o `terraform apply my_vm.out`.
  - Esse comando aplica as mudan√ßas especificadas no arquivo de plano gerado pelo comando `terraform plan`.

```powershell
terraform apply my_vm.out
```

![Terraform-Apply](/assets/img/Lab03-VMWindowsServer/TerraformApply+Outputs.png){: .shadow .rounded-10}

<!-- #### 9. Validar as informa√ß√µes no Output

- Vo√ß√™ pode executar o comando *terraform output*. Ele √© utilizado para exibir os valores das vari√°veis de sa√≠da definidas no seu arquivo de configura√ß√£o do Terraform. Essas vari√°veis de sa√≠da permitem que voc√™ acesse e use os valores gerados pela execu√ß√£o do plano de Terraform, como endere√ßos IP, IDs de recursos, URLs e outras informa√ß√µes importantes.

```powershell
terraform output
```

![Terraform-Output](/assets/img/Lab02-ServicePrincipal/terraform%20output.png){: .shadow .rounded-10}

 -->

#### 09. Listar recursos criados pelo Terraform

- Vamos executar o comando `terraform state list`. Ele √© utilizado para listar todos os recursos gerenciados pelo estado do Terraform.
  - Este comando exibe uma lista de recursos que foram criados, atualizados ou destru√≠dos pelo Terraform e que est√£o sendo rastreados no arquivo tfstate.

```powershell
terraform state list
```

![Terraform-State-List](/assets/img/Lab03-VMWindowsServer/TerraformStateList.png){: .shadow .rounded-10}

#### 11. Validar recursos criados no Portal Azure

- A estrutura de recursos criada pode ser vista na figura abaixo.

![RG-Recursos](/assets/img/Lab03-VMWindowsServer/RG+Recursos.png){: .shadow .rounded-10}

- Os detalhes da VM criada podem ser vistos na figura abaixo.

![VM-Portal](/assets/img/Lab03-VMWindowsServer/Valida√ß√£oVMCriadanoPortal.png){: .shadow .rounded-10}

- A regra de seguran√ßa de rede (NSG) foi criada com origem somente meu IP p√∫blico e destino o IP da VM, como visto na figura abaixo.

![NSG-Rule](/assets/img/Lab03-VMWindowsServer/NSGRule.png){: .shadow .rounded-10}


#### 12. Acessar VM criada

- Nesta parte, com o usuario e senha em m√£os, podemos fazer o teste de acesso √† VM atrav√©s do ***RDP - Remote Desktop***.

![VM-Access](/assets/img/Lab03-VMWindowsServer/AcessoWindowsServer.png){: .shadow .rounded-10}

#### 12. Remover recursos criados com Terraform Destroy

- Agora que n√≥s criamos a VM, testamos, vamos fazer a remo√ß√£o dela. O comando `terraform destroy` √© utilizado para destruir todos os recursos gerenciados pelo Terraform em sua configura√ß√£o. Isso significa que ele apagar√° todos os recursos da infraestrutura que foram criados ou gerenciados pelo Terraform.

> Esse comando √© √∫til quando voc√™ deseja desfazer todas as mudan√ßas aplicadas ou quando precisa limpar o ambiente.
{: .prompt-tip }

![Terraform-Destroy](/assets/img/Lab03-VMWindowsServer/TerraformDestroy1.png){: .shadow .rounded-10}
![Terraform-Destroy2](/assets/img/Lab03-VMWindowsServer/TerraformDestroy2.png){: .shadow .rounded-10}

> √â importante lembrar que o comando terraform destroy apaga todos os recursos gerenciados pelo Terraform, ent√£o use-o com cautela, especialmente em ambientes de produ√ß√£o. Sempre revise o plano de destrui√ß√£o antes de confirmar para garantir que voc√™ n√£o est√° apagando algo por engano.
{: .prompt-danger }

#### 13. Seguran√ßa e Boas Pr√°ticas

**Armazenamento Seguro de Credenciais**: √â recomend√°vel usar o "*Azure Key Vault*" para armazenar informa√ß√µes sens√≠veis, como Senhas, IDs de cliente, segredos e certificados. Para integr√°-lo ao Terraform, use a <a href="https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/data-sources/key_vault_secret" target="_blank">provider oficial do Azure</a>. Isso garante que as credenciais sejam acessadas de forma segura e centralizada.

**Exemplo:**

```hcl
data "azurerm_key_vault_secret" "example" {
  name         = "admin-passwork"
  key_vault_id = azurerm_key_vault.example.id
}
```
**Rota√ß√£o de Secrets**: Configure pol√≠ticas de rota√ß√£o peri√≥dica de segredos no Azure Key Vault ou automatize esse processo para minimizar riscos de seguran√ßa.

### Conclus√£o

<!-- Utilizar o Service Principal no Azure n√£o apenas automatiza processos, mas tamb√©m fortalece a seguran√ßa ao garantir que as permiss√µes sejam estritamente necess√°rias. Com o Terraform, podemos criar e gerenciar essa identidade de forma eficiente, mantendo pr√°ticas de governan√ßa e controle de acesso. Agora, √© hora de aplicar esses conceitos na sua infraestrutura, aprimorando sua automa√ß√£o e seguran√ßa. -->

### Resumo

<!-- Neste artigo, exploramos o processo de cria√ß√£o de um Service Principal no Azure utilizando o Terraform. Abordamos desde os pr√©-requisitos necess√°rios, como uma conta ativa no Azure e a instala√ß√£o do Terraform, at√© a configura√ß√£o das vari√°veis de ambiente no Windows. Passo a passo, demonstramos como preparar o ambiente de trabalho, criar a estrutura de arquivos no VSCode e definir as vari√°veis de ambiente essenciais para o funcionamento adequado do Terraform. Essas etapas s√£o fundamentais para garantir uma automa√ß√£o eficiente e segura na gest√£o de recursos no Azure. -->

### Pr√≥ximos Passos

Para aprofundar seu conhecimento e expandir as possibilidades de automa√ß√£o com Terraform e Azure, considere os seguintes passos:

<!-- - **<a href="https://learn.microsoft.com/en-us/azure/developer/terraform/authenticate-to-azure?tabs=bash" target="_blank">Autentica√ß√£o do Terraform no Azure</a>**: Aprenda diferentes m√©todos de autentica√ß√£o do Terraform no Azure, incluindo o uso de Service Principals e identidades gerenciadas.

- **<a href="https://learn.microsoft.com/en-us/azure/databricks/dev-tools/terraform/service-principals" target="_blank">Provisionamento de Service Principals com Terraform</a>**: Explore como o Terraform pode ser utilizado para provisionar Service Principals, facilitando cen√°rios de automa√ß√£o no Azure Databricks.

- **<a href="https://learn.microsoft.com/en-us/answers/questions/1250584/what-permissions-does-my-service-principal-need-to" target="_blank">Gerenciamento de Permiss√µes de Service Principals</a>**: Entenda as permiss√µes necess√°rias para que um Service Principal possa interagir com recursos como o Azure Key Vault, garantindo seguran√ßa e conformidade.

- **<a href="https://learn.microsoft.com/en-us/azure/aks/learn/quick-kubernetes-deploy-terraform?pivots=development-environment-azure-cli" target="_blank">Integra√ß√£o do Terraform com o Azure Kubernetes Service (AKS)</a>**: Descubra como implantar clusters do AKS usando o Terraform, ampliando suas habilidades em orquestra√ß√£o de cont√™ineres no Azure.
 -->
At√© a pr√≥xima!! üòâ

![resource-group](/assets/img/02/cloudinsights3.png){: .shadow .rounded-10}

#CloudInsights #Azure #Tech #Cloud #Security #Network #Terraform #EntraID

---

[![Build and Deploy](https://github.com/williamcrcosta/williamcosta.github.io/actions/workflows/pages-deploy.yml/badge.svg)](https://github.com/williamcrcosta/williamcosta.github.io/actions/workflows/pages-deploy.yml)
